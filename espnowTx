#include <esp_now.h>
#include <WiFi.h>
#include "I2Cdev.h"
#include "MPU6050.h"
#include "esp_timer.h"

MPU6050 mpu;
uint8_t broadcastAddress[] = {0xD8,0xBC,0x38,0xFB,0x56,0x64};

typedef struct {
    int64_t t;
    float AccX, AccY, AccZ;
    float GyroX, GyroY, GyroZ;
} dataTx;

dataTx Tx;

float gyroXOffset=0, gyroYOffset=0, gyroZOffset=0;
esp_timer_handle_t periodic_timer;

void samplingTask(void* arg){
  int16_t ax,ay,az,gx,gy,gz;
  mpu.getMotion6(&ax,&ay,&az,&gx,&gy,&gz);

  Tx.AccX = ax/16384.0;
  Tx.AccY = ay/16384.0;
  Tx.AccZ = az/16384.0;

  Tx.GyroX = gx/131.0 - gyroXOffset;
  Tx.GyroY = gy/131.0 - gyroYOffset;
  Tx.GyroZ = gz/131.0 - gyroZOffset;

  Tx.t = esp_timer_get_time();

  esp_err_t result = esp_now_send(broadcastAddress, (uint8_t*)&Tx, sizeof(Tx));
  if(result != ESP_OK) Serial.printf("Send failed: %d\n", result);
}

void setup(){
  Serial.begin(115200);
  Wire.begin();  // Important for ESP32
  mpu.initialize();
  if(!mpu.testConnection()){ Serial.println("MPU6050 not found!"); while(1); }
  
  WiFi.mode(WIFI_STA);
  if(esp_now_init() != ESP_OK){ Serial.println("ESP-NOW init failed"); while(1); }
  
  esp_now_peer_info_t peerInfo = {};
  memcpy(peerInfo.peer_addr, broadcastAddress, 6);
  peerInfo.channel = 1;
  peerInfo.encrypt = false;
  esp_now_add_peer(&peerInfo);

  // calibrate gyro
  const int samples=500; float sumX=0,sumY=0,sumZ=0;
  int16_t ax,ay,az,gx,gy,gz;
  for(int i=0;i<samples;i++){
    mpu.getMotion6(&ax,&ay,&az,&gx,&gy,&gz);
    sumX+=gx/131.0; sumY+=gy/131.0; sumZ+=gz/131.0;
    delay(5);
  }
  gyroXOffset=sumX/samples; gyroYOffset=sumY/samples; gyroZOffset=sumZ/samples;

  // periodic timer
  const esp_timer_create_args_t periodic_timer_args = {
      .callback = &samplingTask,
      .arg = NULL,
      .name = "sampling_timer"
  };
  esp_timer_create(&periodic_timer_args, &periodic_timer);
  esp_timer_start_periodic(periodic_timer, 1000); // 1 kHz
}

void loop(){}
